import aQute.bnd.gradle.Bundle

plugins {
    id("java")
    id("java-library")
    id("maven-publish")
    id("jacoco")
//    id("biz.aQute.bnd.builder") version "5.1.2"
    id("biz.aQute.bnd.builder") version "7.1.0"
    id("org.gradle.test-retry") version "1.6.4"
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
    id("signing")
}

def jarVersion = "2.25.2"
group = 'io.nats'

def isRelease = System.getenv("BUILD_EVENT") == "release"
def brn = System.getenv("BRANCH_REF_NAME")
def snap = brn == null || brn.equals("") ? "-SNAPSHOT" : "." + brn + "-SNAPSHOT"

def tc = System.getenv("TARGET_COMPATIBILITY");
def targetCompat = tc == "21" ? JavaVersion.VERSION_21 : (tc == "17" ? JavaVersion.VERSION_17 : JavaVersion.VERSION_1_8)
def jarEnd =       tc == "21" ?  "-jdk21"              : (tc == "17" ? "-jdk17"               : "")
def jarAndArtifactName = "jnats" + jarEnd

version = isRelease ? jarVersion : jarVersion + snap // version is the variable the build actually uses.

def os = System.getProperty("os.name")
def isNotWindows = !os.contains("Windows")
def javaVersion = System.getProperty("java.version");
def compilerIsJava8 = javaVersion.startsWith("1.8")

System.out.println("OS: " + os)
System.out.println("Java: " + javaVersion)
System.out.println("Target Compatibility: " + targetCompat)
System.out.println("Output: " + group + ":" + jarAndArtifactName + ":" + version)

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = targetCompat
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url="https://repo1.maven.org/maven2/" }
    maven { url="https://central.sonatype.com/repository/maven-snapshots/" }
}

dependencies {
    implementation 'org.bouncycastle:bcprov-lts8on:2.73.9'
    implementation 'org.jspecify:jspecify:1.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.1'
    testImplementation 'io.nats:jnats-server-runner:3.0.2'
    if (compilerIsJava8) {
        testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.12.3'
    }
    else {
        testImplementation 'nl.jqno.equalsverifier:equalsverifier:4.2.2'
    }

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java','src/examples/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

tasks.register('bundle', Bundle) {
    from sourceSets.main.output
    exclude("io/nats/examples/**")
}

jar {
    manifest {
        attributes('Automatic-Module-Name': 'io.nats.jnats')
    }
    bundle {
        bnd("Bundle-Name": "io.nats.jnats",
                "Bundle-Vendor": "nats.io",
                "Bundle-Description": "Java Nats",
                "Bundle-DocURL": "https://github.com/nats-io/nats.java"
        )
    }
    archiveBaseName = jarAndArtifactName
}

test {
    useJUnitPlatform()
    maxHeapSize = "2g"
    testLogging {
        exceptionFormat = 'full'
        events "started", "passed", "skipped", "failed"
        showStandardStreams = true
    }
    retry {
        failOnPassedAfterRetry = false
        maxFailures = 4
        maxRetries = 4
    }
    int mpf = Math.max(1, (int)(Runtime.runtime.availableProcessors() * 3 / 4))
    if (compilerIsJava8 || isNotWindows) {
        maxParallelForks = Math.min(6, mpf)
    }
    else {
        maxParallelForks = Math.min(3, mpf);
    }
    systemProperty 'junit.jupiter.execution.timeout.default', '3m'
}

javadoc {
    options.overview = 'src/main/javadoc/overview.html' // relative to source root
    options.memberLevel = JavadocMemberLevel.PUBLIC
    source = sourceSets.main.allJava
    title = "NATS.IO Java API"
    excludes = [
            "io/nats/client/impl",
            "io/nats/examples",
            "io/nats/client/api/ConsumerCreateRequest.java",
            "io/nats/client/api/MessageGetRequest.java",
            "io/nats/client/api/MessageDeleteRequest.java",
            "io/nats/client/support/IncomingHeadersProcessor.java",
            "io/nats/client/support/**",
            "**/Debug**"
    ]
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('examplesJar', Jar) {
    archiveClassifier.set('examples')
    manifest {
        attributes('Implementation-Title': 'Java Nats Examples',
                'Implementation-Version': jarVersion,
                'Implementation-Vendor': 'nats.io')
    }
    from(sourceSets.main.output) {
        include "io/nats/examples/**"
    }
}

tasks.register('javadocJar', Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

tasks.register('testsJar', Jar) {
    archiveClassifier.set('tests')
    from sourceSets.test.allSource
}

tasks.register('copyToLib', Copy) {
    into "build/libs"
    from configurations.runtimeClasspath
}

tasks.register('uberJar', Jar) {
    archiveClassifier = 'uber'

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }

    exclude ('META-INF/**')

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    dependsOn copyToLib
}

artifacts {
    archives javadocJar, sourcesJar, examplesJar, testsJar
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required = true // coveralls plugin depends on xml format report
        html.required = true
    }
    afterEvaluate { // only report on main library not examples
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/examples/**', '**/Debug**'])
        }))
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            username = System.getenv('OSSRH_USERNAME')
            password = System.getenv('OSSRH_PASSWORD')
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact examplesJar
            artifact javadocJar
            artifact testsJar
            pom {
                name = jarAndArtifactName
                packaging = "jar"
                groupId = group
                artifactId = jarAndArtifactName
                description = 'Client library for working with the NATS messaging system.'
                url = 'https://github.com/nats-io/nats.java'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = "synadia"
                        name = "Synadia"
                        email = "info@synadia.com"
                        url = "https://nats.io"
                    }
                }
                scm {
                    url = 'https://github.com/nats-io/nats.java'
                }
            }
        }
    }
}

if (isRelease) {
    signing {
        def signingKeyId = System.getenv('SIGNING_KEY_ID')
        def signingKey = System.getenv('SIGNING_KEY')
        def signingPassword = System.getenv('SIGNING_PASSWORD')
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign configurations.archives
        sign publishing.publications.mavenJava
    }
}
